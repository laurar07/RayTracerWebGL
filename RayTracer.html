<html>

<head>
<title>Ray tracer using WebGL</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script id="shader-fs" type="x-shader/x-fragment">
  precision mediump float;

  const vec3 lightDir = vec3(0.577350269, 0.577350269, -0.577350269);
  varying vec3 vPosition;
  uniform vec3 cameraPos;
  uniform vec3 sphere1Center;
  uniform vec3 sphere2Center;
  uniform vec3 sphere3Center;
  uniform vec3 triangleVertex1;
  uniform vec3 triangleVertex2;
  uniform vec3 triangleVertex3;

  bool intersectSphere(vec3 center, vec3 lStart, vec3 lDir,
                       out float dist) {
					   
	//Spheres have been designed with radius of 1.0
    vec3 c = center - lStart;
    float b = dot(lDir, c);
	float d = b*b - dot(c, c) + 1.0; //First, calculate the determinant
    if (d < 0.0) {
		//If the determinant is zero, no intersection
		dist = 10000.0; //Place the distance at a really high value
		return false;
    }

	//Calculate the root of the equation
    dist = b - sqrt(d);
	
    if (dist < 0.0) {
	  //If the root is negative, no intersection
      dist = 10000.0; //Place the distance at a really high value 
      return false;
    }

	//If we are here, we found an intersection
    return true;
  }
  
  bool defineContainingPlane(vec3 v1, vec3 v2, vec3 v3, out float A, out float B, out float C, out float D) {
	
	//First, draw a normal from 2 vectors
	vec3 v1v2 = normalize(v2 - v1);
	vec3 v1v3 = normalize(v3 - v1);
	
	vec3 normalToPlane = normalize(cross(v1v2, v1v3));
	
	//The equation of the plane is Ax + By + Cz + D = 0;
	A = normalToPlane.x;
	B = normalToPlane.y;
	C = normalToPlane.z;
	
	//Take any point to calculate D
	//D = -(Ax + By + Cz)
	
	D = -1.0 * (A * v2.x + B * v2.y + C * v2.z);
	
	return true;
  
  }
  
  bool rayPlaneIntersection(vec3 rayPos, vec3 rayDir, float A, float B, float C, float D, out vec3 intersect) {
  
	float denominator = A * rayDir.x + B * rayDir.y + C * rayDir.z;
	if (denominator == 0.0)
		return false;
	float numerator = -1.0 * (A * rayPos.x + B * rayPos.y + C * rayPos.z + D);
	
	float t = numerator / denominator;

	intersect.x = rayPos.x + t * rayDir.x;
	intersect.y = rayPos.y + t * rayDir.y;
	intersect.z = rayPos.z + t * rayDir.z;
	
	return true;
  }
  
  bool get2Dcoordinates(vec3 vertex1, vec3 vertex2, vec3 vertex3, out float vec1x, out float vec1y, out float vec2x, out float vec2y, out float vec3x, out float vec3y, out float result)
  {
	//First, project onto z = 0 plane
	result = (0.5) * ((vertex2.x - vertex1.x) * (vertex3.y - vertex1.y) - (vertex3.x - vertex1.x) * (vertex2.y - vertex1.y));
	if (result != 0.0) {
		vec1x = vertex1.x;
		vec1y = vertex1.y;
		vec2x = vertex2.x;
		vec2y = vertex2.y;
		vec3x = vertex3.x;
		vec3y = vertex3.y;
		
		return true;
	}
		
    //Project onto y = 0;
	result = (0.5) * ((vertex2.x - vertex1.x) * (vertex3.z - vertex1.z) - (vertex3.x - vertex1.x) * (vertex2.z - vertex1.z));
	if (result != 0.0) {
		vec1x = vertex1.x;
		vec1y = vertex1.z;
		vec2x = vertex2.x;
		vec2y = vertex2.z;
		vec3x = vertex3.x;
		vec3y = vertex3.z;
		
		return true;
	}
	
	//Project onto x = 0
	result = (0.5) * ((vertex2.y - vertex1.y) * (vertex3.z - vertex1.z) - (vertex3.y - vertex1.y) * (vertex2.z - vertex1.z));
	if (result != 0.0) {
		vec1x = vertex1.y;
		vec1y = vertex1.z;
		vec2x = vertex2.y;
		vec2y = vertex2.z;
		vec3x = vertex3.y;
		vec3y = vertex3.z;
		
		return true;
	}
	
	return false;
  }
  
  float area(float p1x, float p1y, float p2x, float p2y, float p3x, float p3y)
  {

	float result = (0.5) * ((p2x - p1x) * (p3y - p1y) - (p3x - p1x) * (p2y - p1y));
	return result;
  }
  
  bool computeAreas(vec3 p, float p1x, float p1y, float p2x, float p2y, float p3x, float p3y, float denom)
  {
	float num_alpha = area(p.x, p.y, p2x, p2y, p3x, p3y);
	float alpha = num_alpha / denom;

	float num_beta = area(p1x, p1y, p.x, p.y, p3x, p3y);
	float beta = num_beta / denom;
	
	float num_gamma = area(p1x, p1y, p2x, p2y, p.x, p.y);
	float gamma = num_gamma / denom;

	//float total = alpha + beta + gamma;
	
	if(alpha > 0.0 && beta > 0.0 && gamma > 0.0) {
		return true;
	}
	else
		return false;
  }
  
  bool intersectTriangle(vec3 v1, vec3 v2, vec3 v3, vec3 lStart, vec3 lDir, out float dist, out vec3 triangle_norm) {
  
	//First, define containing plane
	float A, B, C, D;
	if (!(defineContainingPlane(v1, v2, v3, A, B, C, D))) {
		dist = 10000.0; 
		return false;
	}
	
	//Get the intersection with the plane
	vec3 intersectPt;
	if(!(rayPlaneIntersection(lStart, lDir, A, B, C, D, intersectPt))) {
		dist = 10000.0; 
		return false;
	}
	
	//Get 2D coordinates
	
	float vec1x, vec1y, vec2x, vec2y, vec3x, vec3y, denom;
	get2Dcoordinates(v1, v2, v3, vec1x, vec1y, vec2x, vec2y, vec3x, vec3y, denom);
	
	//result = sqrt(pow((p.x - ray.origin[0]), 2) + pow((curr_min.y - ray.origin[1]), 2) + pow((curr_min.z - ray.origin[2]), 2));
	//Check if the intersection is inside the triangle
	
	if(!(computeAreas(intersectPt, vec1x, vec1y, vec2x, vec2y, vec3x, vec3y, denom))) {
		dist = 10000.0; 
		return false;
	}
	
	dist = sqrt(pow((intersectPt.x - lStart.x), 2.0) + pow((intersectPt.y - lStart.y), 2.0) + pow((intersectPt.z - lStart.z), 2.0));
	
	triangle_norm.x = A; triangle_norm.y = B; triangle_norm.z = C;
	
	//If we are here, we found an intersection
	return true;
  
  }

  vec3 lightAt(vec3 N, vec3 V, vec3 color) {
    vec3 L = lightDir;
    vec3 R = reflect(-L, N);

    float c = 0.3 + 0.4 * pow(max(dot(R, V), 0.0), 30.0) + 0.7 * dot(L, N);

    if (c > 1.0) {
      return mix(color, vec3(1.6, 1.6, 1.6), c - 1.0);
    }

    return c * color;
  }

  bool intersectWorld(vec3 lStart, vec3 lDir, out vec3 pos,
                      out vec3 normal, out vec3 color) {
    float d1, d2, d3, d4;
    bool h1, h2, h3, h4;
	vec3 triangle_norm;	
	//Calculate the intersections with 3 spheres
	//d1, d2, d3 represent the distances
    h1 = intersectSphere(sphere1Center, lStart, lDir, d1);
    h2 = intersectSphere(sphere2Center, lStart, lDir, d2);
    h3 = intersectSphere(sphere3Center, lStart, lDir, d3);
	h4 = intersectTriangle(triangleVertex1, triangleVertex2, triangleVertex3, lStart, lDir, d4, triangle_norm);

    if (h1 && d1 < d2 && d1 < d3 && d1 < d4) {
		//Sphere1 intersected and the intersection is the smallest distance
		pos = lStart + d1 * lDir;
		normal = pos - sphere1Center;
		color = vec3(0.0, 0.0, 0.9);
    }
    else if (h2 && d2 < d3 && d2 < d4) {
		//Sphere2 intersected and the intersection is the smallest distance
		pos = lStart + d2 * lDir;
		normal = pos - sphere2Center;
		color = vec3(0.9, 0.0, 0.0);
    }
    else if (h3 && d3 < d4) {
		//Sphere3 intersected
		pos = lStart + d3 * lDir;
		normal = pos - sphere3Center;
		color = vec3(0.0, 0.9, 0.0);
    }
	else if(h4) {
		//Triangle intersected
		pos = lStart + d4 * lDir;
		//normal = pos - sphere3Center;
		normal = triangle_norm;
		color = vec3(0.0, 1.0, 0.0);
	}
    else if (lDir.y < -0.01) {
		//No sphere intersections
		//This checks intersections with the floor
		pos = lStart + ((lStart.y + 2.7) / -lDir.y) * lDir;
		if (pos.x*pos.x + pos.z*pos.z > 30.0) {
			return false;
		}
		normal = vec3(0.0, 1.0, 0.0);
		if (fract(pos.x / 5.0) > 0.5 == fract(pos.z / 5.0) > 0.5) {
			color = vec3(1.0);
		}
		else {
			color = vec3(0.0);
		}
	}
    else {
		//No intersections with spheres or floor -- return false
		return false;
    }

    return true;
  }

  void main(void)
  {
    vec3 cameraDir = normalize(vPosition - cameraPos);

    vec3 p1, norm, p2;
    vec3 col, colT, colM, col3;
	//Shoot the first ray from the camera location -- get position, normal and color
    if (intersectWorld(cameraPos, cameraDir, p1,
                       norm, colT)) {
      col = lightAt(norm, -cameraDir, colT);
      colM = (colT + vec3(0.7)) / 1.7;
      cameraDir = reflect(cameraDir, norm); //Reflect the ray from the camera
      if (intersectWorld(p1, cameraDir, p2, norm, colT)) {
        col += lightAt(norm, -cameraDir, colT) * colM;
        colM *= (colT + vec3(0.7)) / 1.7;
        cameraDir = reflect(cameraDir, norm); //Reflect the ray from the camera
        if (intersectWorld(p2, cameraDir, p1, norm, colT)) {
          col += lightAt(norm, -cameraDir, colT) * colM;
        }
      }

      gl_FragColor = vec4(col, 1.0);
    }
    else {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    }
  }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
  attribute vec2 aVertexPosition;
  attribute vec3 aPlotPosition;

  varying vec3 vPosition;

  void main(void)
  {
    gl_Position = vec4(aVertexPosition, 1.0, 1.0);
    vPosition = aPlotPosition;
 }
</script>


<script type="application/x-javascript">

  var gl;
  function initGL(canvas) {
    try {
      gl = canvas.getContext("experimental-webgl");
      gl.viewport(0, 0, canvas.width, canvas.height);
    } catch(e) {
    }
    if (!gl) {
      alert("Could not initialise WebGL, sorry :-(");
    }
  }

  function getShader(gl, id)
  {
      var shaderScript = document.getElementById(id);
      if (!shaderScript)
          return null;

      var shader;
      if (shaderScript.type == "x-shader/x-fragment")
      {
          shader = gl.createShader(gl.FRAGMENT_SHADER);
      }
      else if (shaderScript.type == "x-shader/x-vertex")
      {
          shader = gl.createShader(gl.VERTEX_SHADER);
      }
      else
      {
          return null;
      }

      gl.shaderSource(shader, shaderScript.textContent);
      gl.compileShader(shader);

      if (!gl.getShaderParameter(shader, gl.COMPILE_STATUS))
      {
          alert(gl.getShaderInfoLog(shader));
          return null;
      }

      return shader;
  }

  var shaderProgram;
  var aVertexPosition;
  function initShaders()
  {
    var fragmentShader = getShader(gl, "shader-fs");
    var vertexShader = getShader(gl, "shader-vs");

    shaderProgram = gl.createProgram();
    gl.attachShader(shaderProgram, vertexShader);
    gl.attachShader(shaderProgram, fragmentShader);
    gl.linkProgram(shaderProgram);

    if (!gl.getProgramParameter(shaderProgram, gl.LINK_STATUS))
    {
      alert("Could not initialize shaders");
    }

    gl.useProgram(shaderProgram);

    aVertexPosition = gl.getAttribLocation(shaderProgram, "aVertexPosition");
    gl.enableVertexAttribArray(aVertexPosition);

    aPlotPosition = gl.getAttribLocation(shaderProgram, "aPlotPosition");
    gl.enableVertexAttribArray(aPlotPosition);

    cameraPos = gl.getUniformLocation(shaderProgram, "cameraPos");
    
	//Set up the spheres
	sphere1Center = gl.getUniformLocation(shaderProgram, "sphere1Center");
    sphere2Center = gl.getUniformLocation(shaderProgram, "sphere2Center");
    sphere3Center = gl.getUniformLocation(shaderProgram, "sphere3Center");
	
	//Set up the triangle
	triangleVertex1 = gl.getUniformLocation(shaderProgram, "triangleVertex1");
	triangleVertex2 = gl.getUniformLocation(shaderProgram, "triangleVertex2");
	triangleVertex3 = gl.getUniformLocation(shaderProgram, "triangleVertex3");
  }


  function initBuffers()
  {
    vertexPositionBuffer = gl.createBuffer();
    gl.bindBuffer(gl.ARRAY_BUFFER, vertexPositionBuffer);
    var vertices = [
         1.0,  1.0,
        -1.0,  1.0,
         1.0, -1.0,
        -1.0, -1.0,
    ];
    gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(vertices), gl.STATIC_DRAW);
    gl.bindBuffer(gl.ARRAY_BUFFER, vertexPositionBuffer);
    gl.vertexAttribPointer(aVertexPosition, 2, gl.FLOAT, false, 0, 0);


    var plotPositionBuffer = gl.createBuffer();
    gl.bindBuffer(gl.ARRAY_BUFFER, plotPositionBuffer);
    gl.vertexAttribPointer(aPlotPosition, 3, gl.FLOAT, false, 0, 0);
  }

  function crossProd(v1, v2) {
    return { x: v1.y*v2.z - v2.y*v1.z,
             y: v1.z*v2.x - v2.z*v1.x,
             z: v1.x*v2.y - v2.x*v1.y };
  }

  function normalize(v) {
    l = Math.sqrt(v.x*v.x + v.y*v.y + v.z*v.z);
    return { x: v.x/l, y: v.y/l, z: v.z/l };
  }

  function vectAdd(v1, v2) {
    return { x: v1.x + v2.x, y: v1.y + v2.y, z: v1.z + v2.z };
  }

  function vectSub(v1, v2) {
    return { x: v1.x - v2.x, y: v1.y - v2.y, z: v1.z - v2.z };
  }

  function vectMul(v, l) {
    return { x: v.x*l, y: v.y*l, z: v.z*l };
  }

  function pushVec(v, arr) {
    arr.push(v.x, v.y, v.z);
  }

  t = 0;
  function drawScene()
  {
  /*
    x1 = Math.sin(t * 1.1) * 1.5;
    y1 = Math.cos(t * 1.3) * 1.5;
    z1 = Math.sin(t + Math.PI/3) * 1.5;
    x2 = Math.cos(t * 1.2) * 1.5;
    y2 = Math.sin(t * 1.4) * 1.5;
    z2 = Math.sin(t*1.25 - Math.PI/3) * 1.5;
    x3 = Math.cos(t * 1.15) * 1.5;
    y3 = Math.sin(t * 1.37) * 1.5;
    z3 = Math.sin(t*1.27) * 1.5;
	*/
	x1 = Math.sin(0.0) * 1.5;
    y1 = Math.cos(0.0) * 1.5;
    z1 = Math.sin(Math.PI/3) * 1.5;
    x2 = Math.cos(0.0) * 1.5;
    y2 = Math.sin(0.0) * 1.5;
    z2 = Math.sin(- Math.PI/3) * 1.5;
    x3 = Math.cos(0.0) * 1.5;
    y3 = Math.sin(0.0) * 1.5;
    z3 = Math.sin(0.0) * 1.5;
	
	//Define the points for the triangle
	x4 = Math.sin(t * 1.5) * 1.5;
	y4 = Math.cos(t * 1.7) * 1.5;
	z4 = Math.sin(t*1.5 - Math.PI/3) * 1.5;
	
	x5 = Math.sin(t * 1.7) * 1.5;
	y5 = Math.cos(t * 1.9) * 1.5;
	z5 = Math.sin(t*1.75 - Math.PI/3) * 1.5;
	
	x6 = Math.sin(t * 1.9) * 1.5;
	y6 = Math.cos(t * 2.1) * 1.5;
	z6 = Math.sin(t*2.00 - Math.PI/3) * 1.5;

    cameraFrom = { x: Math.sin(t * 0.4) * 18,
                   y: Math.sin(t * 0.13) * 5 + 5,
                   z: Math.cos(t * 0.4) * 18 };
    cameraTo = { x:0, y:0, z:0 };
    cameraPersp = 6;
    up = { x: 0, y: 1, z: 0 };
    cameraDir = normalize(vectSub(cameraTo, cameraFrom));

    cameraLeft = normalize(crossProd(cameraDir, up));
    cameraUp = normalize(crossProd(cameraLeft, cameraDir));
    // cameraFrom + cameraDir * cameraPersp
    cameraCenter = vectAdd(cameraFrom, vectMul(cameraDir, cameraPersp));
    // cameraCenter + cameraUp + cameraLeft * ratio
    cameraTopLeft  = vectAdd(vectAdd(cameraCenter, cameraUp),
                             vectMul(cameraLeft, ratio));
    cameraBotLeft  = vectAdd(vectSub(cameraCenter, cameraUp),
                             vectMul(cameraLeft, ratio));
    cameraTopRight = vectSub(vectAdd(cameraCenter, cameraUp),
                             vectMul(cameraLeft, ratio));
    cameraBotRight = vectSub(vectSub(cameraCenter, cameraUp),
                             vectMul(cameraLeft, ratio));


    //corners = [1.2, 1, -12, -1.2, 1, -12, 1.2, -1, -12, -1.2, -1, -12];
    corners = [];
    pushVec(cameraTopRight, corners);
    pushVec(cameraTopLeft, corners);
    pushVec(cameraBotRight, corners);
    pushVec(cameraBotLeft, corners);

    gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(corners), gl.STATIC_DRAW);

    gl.uniform3f(cameraPos, cameraFrom.x, cameraFrom.y, cameraFrom.z);
    gl.uniform3f(sphere1Center, x1, y1, z1);
    gl.uniform3f(sphere2Center, x2, y2, z2);
    gl.uniform3f(sphere3Center, x3, y3, z3);
	
	//Setup the triangle
	gl.uniform3f(triangleVertex1, x4, y4, z4);
	gl.uniform3f(triangleVertex2, x5, y5, z5);
	gl.uniform3f(triangleVertex3, x6, y6, z6);

    gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);

	
    t += 0.03;
    if (t > Math.PI * 200) {
      t -= Math.PI * 200;
    }
	
  }

  var timer = 0;

  var canvas;
  function webGLStart()
  {
    canvas = document.getElementById("canvas");
    initGL(canvas);
    initShaders()

    gl.clearColor(0.0, 0.0, 0.0, 1.0);

    gl.clearDepth(1.0);

    initBuffers();
  }

  function flipAnim()
  {
    if (timer) {
      clearInterval(timer);
      timer = 0;
    }
    else {
      timer = setInterval(drawScene, 15);
    }
  }

  var ratio;
  function resizeCanvas(w)
  {
    if (w == -1) {
      document.getElementById('contrib').style.display = "none";
      canvas.style.display = "none";
      canvas.parentNode.style.position = "absolute";
      canvas.parentNode.style.top = 0;
      w = canvas.parentNode.parentNode.offsetWidth;
      ratio = w / canvas.parentNode.parentNode.offsetHeight;
      canvas.style.display = "";
    }
    else {
      document.getElementById('contrib').style.display = "";
      ratio = 1.6;
      canvas.parentNode.style.position = "";
      canvas.parentNode.style.top = "";
      window.onresize = null;
    }
    canvas.width = w;
    canvas.height = w / ratio;

    gl.viewport(0, 0, canvas.width, canvas.height);

    t -= 0.03;
    drawScene();
  }

  var resizeTimer = false;
  function fullScreen() {
    window.onresize = function() {
      if (resizeTimer) {
        clearTimeout(resizeTimer);
      }
      resizeTimer = setTimeout(function() {
        fullScreen();
      }, 100);
    };
    
    resizeCanvas(-1);
  }

</script>
</head>


<body onload="webGLStart(); resizeCanvas(600); flipAnim()" style="height: 100%; margin: 0">
  <div style="height:100%; margin: 0;">
    <div style="text-align:center; padding: 1em;">
      <input type=button onclick="resizeCanvas(400)" value=small>
      <input type=button onclick="resizeCanvas(600)" value=medium>
      <input type=button onclick="resizeCanvas(800)" value=large>
      <input type=button onclick="fullScreen();" value="very large">
      &nbsp;&nbsp;&nbsp;&nbsp;
      <input type=button onclick="flipAnim()" value=pause>
    </div>
    <div style="z-index:-1; text-align:center; width:100%;">
      <canvas id="canvas" style="border: none;" width="425" height="330"></canvas>
    </div>
  </div>
  <div style="margin: -8em 1em 0;" id="contrib">
    <p>
      Using the framework from <a href="http://learningwebgl.com/blog/?p=205">this
      example</a> from Giles Thomas.
    </p>
    <p>
      Feel free to shoot me an
      <a href="mailto:jonas&#64;sicking.cc">email</a> if
      you like it or have questions.
    </p>
    <p>
      See also <a href="raycolor.html">this</a> one for a more colorful version.
    </p>
  </div>
</body>

</html>